apply plugin: 'cipher.so'
apply plugin: 'com.android.application'
apply from: 'and_res_guard.gradle'
apply plugin: 'channel'


def channelPath=project.rootDir.path+"/channels/channel.txt"

/**
 * 直接编译生成多渠道包
 */
//通过命令添加渠道参数
//./gradlew channelRelease -Pchannels=yingyongbao,gamecenter
channel {//从此，打包效率提高十倍以上，即使不开FastMode
    //多渠道包的命名规则，默认为：${appName}-${versionName}-${versionCode}-${flavorName}-${buildType}
    apkNameFormat='fandengdushuhui_${flavorName}_${versionName}_${buildType}'
    //指定渠道文件
    channelFile=file(channelPath)
    //多渠道包的输出目录，默认为new File(project.buildDir,"channel")
    baseOutputDir = new File(project.rootDir, "channelApk/release")
    //快速模式：生成渠道包时不进行校验（速度可以提升10倍以上，默认为false）
    isFastMode = true
    //buildTime的时间格式，默认格式：yyyyMMdd-HHmmss
    buildTimeDateFormat = 'yyyyMMdd-HH:mm:ss'
    //低内存模式（仅针对V2签名，默认为false）：只把签名块、中央目录和EOCD读取到内存，不把最大头的内容块读取到内存，在手机上合成APK时，可以使用该模式
    lowMemory = false
}

/**
 * 根据已有基础包重新生成多渠道包
 */
//通过命令添加渠道参数
//./gradlew reBuildChannel -Pchannels=yingyongbao,gamecenter
rebuildChannel {//从此，打包效率提高十倍以上，即使不开FastMode
    //这里的目录都是自己定义的，我这里是动态的设置包的名称的，由于versionName比较长，就截取了一段
//    baseDebugApk = new File(project.buildDir, "app/build/outputs/apk/debug/app-debug.apk")
//    debugOutputDir = new File(project.buildDir, "rebuildChannel/debug")
    channelFile=file(channelPath)
    baseReleaseApk = new File(project.rootDir,"app/build/outputs/apk/release/AndResGuard_app-release/app-release_aligned_signed.apk")
//    baseReleaseApk = new File(project.rootDir,"app/release/fandengdushuhui_dushutest_4.6.2_release.apk");
    releaseOutputDir = new File(project.rootDir, "channelApk/rebuildRelease")
    //快速模式：生成渠道包时不进行校验（速度可以提升10倍以上，默认为false）
    isFastMode = true
    //低内存模式（仅针对V2签名，默认为false）：只把签名块、中央目录和EOCD读取到内存，不把最大头的内容块读取到内存，在手机上合成APK时，可以使用该模式
    lowMemory = false
}

android {

    signingConfigs {
        signApk {
            storeFile file(project.property('storeFile').toString())
            storePassword project.property('storePassword').toString()
            keyAlias project.property('keyAlias').toString()
            keyPassword project.property('keyPassword').toString()
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "com.tuoniao.sijibao"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "1.0.0"
        multiDexEnabled true
        signingConfig signingConfigs.signApk
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        flavorDimensions "default"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

        }
        debug {
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled false
            debuggable(true)
            jniDebuggable(true)
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding{
        enabled true
    }
    compileOptions{
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main {
            java {
                srcDirs 'src/main/java', 'src/main/java-gen'
            }
        }
    }

//    productFlavors{
//        google{
//
//        }
//    }
//
//    android.applicationVariants.all {variant->
//        variant.outputs.all{output->
//            if (output.outputFile != null && output.outputFile.name.endsWith('.apk') && 'release' == variant.buildType.name){
//                def flavorName=variant.flavorName.startsWith("_") ? variant.flavorName.substring(1) : variant.flavorName
//                def fileName="Dushu_${flavorName}_v${variant.versionName}_${buildTime()}.apk"
//                outputFileName=fileName
//            }
//        }
//    }
}

def buildTime(){
    def date=new Date()
    def formattedDate=date.format('yyyyMMdd')
    return formattedDate
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.flyco.tablayout:FlycoTabLayout_Lib:2.1.2@aar'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.github.tbruyelle:rxpermissions:0.10.2'
    implementation 'top.zibin:Luban:1.1.8'
    implementation 'com.leon.channel:helper:2.0.3'

    implementation 'io.reactivex.rxjava2:rxjava:2.2.10'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'de.greenrobot:greendao:1.3.7'
    implementation project(path: ':lib_db')
    implementation project(path: ':utils_fandeng')
    implementation 'org.jooq:joor-java-8:0.9.13'
}

//明文底层加密处理
cipher.so {
    keys {
        hello {
            value = 'Http_url'
        }
        httpsKey {
            value = 'testData'
        }
    }
    encryptSeed = 'tuoniao_sijibao' //用于加密的密钥
}

task assembleMultiChannelApks{
    dependsOn('resguardRelease','reBuildChannel')
    doFirst {
        println("开始执行 --->>> aaaSign2")
    }
    doLast {
        println("结束执行 --->>> aaaSign2")
    }
}